import com.github.sherter.googlejavaformatgradleplugin.GoogleJavaFormat
import com.github.sherter.googlejavaformatgradleplugin.VerifyGoogleJavaFormat

plugins {
    id 'java'
    id 'application'
    id 'jacoco'
    id 'idea'
    id 'eclipse'
    id 'com.palantir.docker' version '0.25.0'
    id 'com.palantir.docker-run' version '0.25.0'
    id 'com.github.johnrengelman.shadow' version '6.1.0'
    id 'com.github.sherter.google-java-format' version '0.9'
}

repositories {
    mavenCentral()
}

project.version = '0.1.1'
sourceCompatibility = JavaVersion.VERSION_11
targetCompatibility = JavaVersion.VERSION_11

dependencies {
    implementation 'org.slf4j:slf4j-api:1.7.+'
    implementation 'ch.qos.logback:logback-core:1.2.+'
    implementation 'ch.qos.logback:logback-classic:1.2.+'
    implementation 'net.logstash.logback:logstash-logback-encoder:6.4'

    testImplementation 'junit:junit:4.13'
    testImplementation 'org.hamcrest:hamcrest-library:1.3'
}

application {
    mainClassName = 'info.akotadakura.App'
}

tasks.withType(JavaCompile).all {
    options.encoding = "UTF-8"
}

jar {
    archiveFileName = "${project.name}.jar"
    manifest {
        attributes 'Main-Class' : 'info.akotadakura.App'
    }
}

shadowJar {
    archiveFileName = "${project.name}.jar"
    dependsOn jacocoTestReport
    finalizedBy tasks.docker
}

docker {
    name "${project.name}:${project.version}"
    dockerfile file('Dockerfile')
    copySpec.from('build/libs').into('build/libs')
    noCache true
    dependsOn tasks.shadowJar
}

dockerRun {
    name project.name
    image "${project.name}:${project.version}"
    daemonize false
    clean true
}

tasks.test.finalizedBy(jacocoTestReport)

jacoco {
    toolVersion = '0.8.5'
}

jacocoTestReport {
    dependsOn test
    finalizedBy shadowJar
    reports {
        xml.enabled false
        csv.enabled false
        html.destination file("${buildDir}/jacocoHtml")
    }
}

googleJavaFormat {
    toolVersion = '1.8'
    options style: 'GOOGLE'
}

task format(type: GoogleJavaFormat){
  //Options
}

task verifyFormat(type: VerifyGoogleJavaFormat) {
  //Options
}